# Maintainer: sn6uv mathics@angusgriffith.com
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: rnestler
# Contributor: mefistofeles

pkgname=mathics
pkgver=0.8
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='General-purpose online computer algebra system featuring Mathematica-compatible syntax and functions'
url='http://www.mathics.org/'
license=('GPL3')
depends=('python2' 'python2-setuptools' 'python2-dateutil' 'python2-ply>=3.4' 'python2-six' 'python2-sympy>=0.7.3' 'python2-django>=1.6' 'python2-colorama' 'python2-argparse')
optdepends=(
    'cython2: cython optimisations'
)

source=("https://github.com/mathics/Mathics/releases/download/v${pkgver}/mathics-${pkgver}.tar.gz")
md5sums=('05d6d1f0af9dc20f267a1d9b1b7b1831')


build() {
    # mathics supports sympy==0.7.6 but works (with minor errors) for >=0.7.6
    cd ${srcdir}/${pkgname}-${pkgver}
    sed -i 's/sympy==0.7.6/sympy>=0.7.3/' setup.py
    sed -i 's/django >= 1.6, < 1.7/django>=1.6/' setup.py
    python2 setup.py build
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    python2 setup.py install --root=${pkgdir}
}
